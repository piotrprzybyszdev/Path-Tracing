function (create_directory_link SRC DST)
    get_filename_component(DST_PATH ${DST} ABSOLUTE)
    if (IS_DIRECTORY ${DST_PATH})
        return()
    endif()

    # Creating a symlink on Windows requires administrative permissions - create junction link instead
    if (WIN32)
        file(TO_NATIVE_PATH ${SRC} WIN_SRC_ASSET)
        file(TO_NATIVE_PATH ${DST} WIN_DST_ASSET)
        execute_process(COMMAND cmd.exe /c mklink /J ${WIN_DST_ASSET} ${WIN_SRC_ASSET})
    else()
        file(CREATE_LINK ${SRC} ${DST} SYMBOLIC)
    endif()
endfunction()

function (download_assets ASSETS ASSET_URLS)
    list(LENGTH ASSETS ASSET_COUNT)
    set(INDEX 1)
    foreach (NAME URL IN ZIP_LISTS ASSETS ASSET_URLS)
        set(ZIP_PATH "${CMAKE_SOURCE_DIR}/assets/${NAME}.zip")
        set(FOLDER_PATH "${CMAKE_SOURCE_DIR}/assets/${NAME}")

        if (NOT EXISTS ${FOLDER_PATH})
            message("Downloading asset: ${FOLDER_PATH} (${INDEX}/${ASSET_COUNT})")
            file(DOWNLOAD ${URL} ${ZIP_PATH})
            file(ARCHIVE_EXTRACT INPUT ${ZIP_PATH} DESTINATION ${FOLDER_PATH})
        else()
            message("Asset already exists: ${FOLDER_PATH} (${INDEX}/${ASSET_COUNT})")
        endif()

        math(EXPR INDEX "${INDEX} + 1")
    endforeach()
endfunction()