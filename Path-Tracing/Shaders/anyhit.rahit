#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : require

#include "ShaderRendererTypes.incl"
#include "common.glsl"

layout(binding = 3, set = 0) uniform sampler2D textures[];

layout(binding = 5, set = 0) readonly buffer GeometryBuffer {
    Geometry[] geometries;
};

layout(binding = 6, set = 0) readonly buffer MetallicRoughnessMaterialBuffer {
    MetallicRoughnessMaterial[] metallicRoughnessMaterials;
};

layout(binding = 7, set = 0) readonly buffer SpecularGlossinessMaterialBuffer {
    SpecularGlossinessMaterial[] specularGlossinessMaterials;
};

layout(shaderRecordEXT, std430) buffer SBT {
    SBTBuffer sbt;
};

layout(location = 0) rayPayloadInEXT Payload payload;
hitAttributeEXT vec3 attribs;

void main()
{
    const vec3 barycentricCoords = computeBarycentricCoords(attribs);

    VertexBuffer vertices = VertexBuffer(geometries[sbt.GeometryIndex].Vertices);
    IndexBuffer indices = IndexBuffer(geometries[sbt.GeometryIndex].Indices);

    const Vertex vertex = getInterpolatedVertex(vertices, indices, gl_PrimitiveID * 3, barycentricCoords);

    uint materialType;
    uint materialIndex = unpackMaterialId(sbt.MaterialId, materialType);

    const uint colorTextureIdx = materialType == MaterialTypeMetallicRoughness ?
        metallicRoughnessMaterials[materialIndex].ColorIdx : specularGlossinessMaterials[materialIndex].DiffuseIdx;
    const vec4 colorFactor = materialType == MaterialTypeMetallicRoughness ?
        metallicRoughnessMaterials[materialIndex].Color : vec4(1.0f);

    const vec4 color = texture(textures[colorTextureIdx], vertex.TexCoords) * colorFactor;

    // Handling decals
    const float dist = gl_RayTmaxEXT;
    if (color.a < 0.5f)
    {
        if (payload.DirectLightPdf == -1.0f || dist < payload.DirectLightPdf)
        {
            payload.LightDirection = color.rgb;
            payload.LightDistance = color.a;
            payload.DirectLightPdf = dist;
        }
        ignoreIntersectionEXT;
    }
}
