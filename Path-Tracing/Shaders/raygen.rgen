#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : require

#include "ShaderRendererTypes.incl"

layout(binding = 0, set = 0) uniform accelerationStructureEXT u_TopLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D u_Image;
layout(binding = 2, set = 0) uniform MainBlock {
    RaygenUniformData mainUniform;
};

layout(binding = 3, set = 0) uniform sampler2D textures[];

layout(binding = 6, set = 0) readonly buffer MetalicRoughnessMaterialBuffer {
    MetalicRoughnessMaterial[] MetalicRoughnessMaterials;
};

layout(binding = 7, set = 0) readonly buffer SpecularGlossinessMaterialBuffer {
    SpecularGlossinessMaterial[] solidColorMaterials;
};

layout(binding = 8, set = 0) uniform LightsBuffer {
    uint u_LightCount;
    DirectionalLight u_DirectionalLight;
    PointLight[MaxLightCount] u_Lights;
};

layout(location = 0) rayPayloadEXT Payload payload;
layout(location = 1) rayPayloadEXT bool isOccluded;

#include "ray.glsl"
#include "shading.glsl"
#include "tracing.glsl"

void sampleMaterial(uint materialId, vec2 texCoords, float lod, out vec3 color, out vec3 emissiveColor, out float roughness, out float metalness)
{
    uint materialType;
    uint materialIndex = unpackMaterialId(materialId, materialType);

    if (materialType == MaterialTypeMetalicRoughness)
    {
        const MetalicRoughnessMaterial material = MetalicRoughnessMaterials[materialIndex];
        emissiveColor = (textureLod(textures[material.EmissiveIdx], texCoords, lod).xyz + material.EmissiveColor) * material.EmissiveIntensity;
        color = textureLod(textures[material.ColorIdx], texCoords, lod).xyz * material.Color;
        roughness = textureLod(textures[material.RoughnessIdx], texCoords, lod).y * material.Roughness;
        metalness = textureLod(textures[material.MetalicIdx], texCoords, lod).z * material.Metalness;
    }
    else
    {
        const SpecularGlossinessMaterial material = solidColorMaterials[materialIndex];
        color = material.Color;
        roughness = DefaultRoughness;
        metalness = DefaultMetalness;
    }
}

bool sampleLight(out vec3 lightDirection, out vec3 lightColor, out float lightDistance, out float attenuation, vec3 position, inout uint rngState)
{
    uint lightIndex = uint(rand(rngState) * (u_LightCount + 1));
    
    if (lightIndex >= u_LightCount)
    {
        lightDirection = u_DirectionalLight.Direction;
        lightColor = u_DirectionalLight.Color;
        lightDistance = DirectionalLightDistance;
        attenuation = 1.0f;
        return true;
    }

    const PointLight light = u_Lights[lightIndex];
    lightDistance = distance(position, light.Position);
    lightDirection = normalize(position - light.Position);
    lightColor = light.Color;
    attenuation = 1.0f / (light.AttenuationConstant + lightDistance * light.AttenuationLinear + lightDistance * lightDistance * light.AttenuationQuadratic);
    return false;
}

void main()
{
    uint rng = initRng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, mainUniform.TotalSamples);

    const Ray primaryRay = constructPrimaryRay(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, mainUniform.Camera);

    vec3 totalLight = vec3(0.0f);

    for (int smpl = 0; smpl < mainUniform.SampleCount; smpl++)
    {
        // NOTE: We are tracing the primary ray for every sample for no reason
        // The result (as of now) is the same each trace
        vec3 throughput = vec3(1.0f);
        Ray ray = primaryRay;

        for (int bounce = 0; bounce < mainUniform.BounceCount; bounce++)
        {
            traceRayEXT(u_TopLevelAS, gl_RayFlagsNoneEXT, 0xff, PrimaryRayHitGroupIndex, 2, PrimaryRayMissGroupIndex, ray.Origin, ray.tmin, ray.Direction, ray.tmax, 0);

            if (payload.HitDistance == MISS_HIT_DISTANCE)
            {
                totalLight += throughput * payload.Position;
                break;
            }
            
            payload.Position = offsetRayOrigin(payload.Position, payload.Normal);

            vec3 color, emissiveColor;
            float roughness, metalness;
            sampleMaterial(payload.MaterialId, payload.TexCoords, payload.Lod, color, emissiveColor, roughness, metalness);

            const vec3 viewDir = ray.Direction;
            const vec3 V = -normalize(viewDir);
            const vec3 N = payload.Normal;
            
            vec3 lightDirection, lightColor;
            float attenuation, lightDistance;

            sampleLight(lightDirection, lightColor, lightDistance, attenuation, payload.Position, rng);

            totalLight += throughput * emissiveColor;

            // TODO: Scale down according to the probability
            if (!checkOccluded(lightDirection, payload.Position, lightDistance))
                totalLight += throughput * computeLightContribution(lightDirection, lightColor, attenuation, payload.Position, V, N, color, roughness, metalness);

            throughput *= color;

            // TODO: Russian rulette termination

            ray.Origin = payload.Position;
            ray.Direction = reflect(ray.Direction, N);

            // TODO: Sample proper BRDF
            vec3 offset = getRandomUnitSphere(rng);
            if (offset == vec3(0.0f))
                offset = N;

            if (dot(N, offset) < 0)
                offset *= -1;
            ray.Direction = normalize(ray.Direction + offset * roughness);
        }
    }
        
    vec3 prevColor = imageLoad(u_Image, ivec2(gl_LaunchIDEXT.xy)).rgb;
    totalLight = (totalLight * mainUniform.Exposure + prevColor * mainUniform.TotalSamples) / (mainUniform.TotalSamples + mainUniform.SampleCount);

    imageStore(u_Image, ivec2(gl_LaunchIDEXT.xy), vec4(totalLight, 1.0f));
}
