#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : require

#include "ShaderRendererTypes.incl"

layout(constant_id = RaygenFlagsConstantId) const uint s_RaygenFlags = RaygenFlagsNone;
layout(constant_id = HitGroupFlagsConstantId) const uint s_HitGroupFlags = HitGroupFlagsNone;

layout(binding = 0, set = 0) uniform accelerationStructureEXT u_TopLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D u_Image;
layout(binding = 2, set = 0) uniform MainBlock {
    RaygenUniformData mainUniform;
};

layout(binding = 3, set = 0) uniform sampler2D textures[];

layout(binding = 6, set = 0) readonly buffer TexturedMaterialBuffer {
    TexturedMaterial[] texturedMaterials;
};

layout(binding = 7, set = 0) readonly buffer SolidColorMaterialBuffer {
    SolidColorMaterial[] solidColorMaterials;
};

layout(binding = 8, set = 0) uniform LightsBuffer {
    uint u_LightCount;
    DirectionalLight u_DirectionalLight;
    PointLight[MaxLightCount] u_Lights;
};

layout(location = 0) rayPayloadEXT Payload payload;
layout(location = 1) rayPayloadEXT bool isOccluded;

#include "shading.glsl"
#include "tracing.glsl"

void sampleMaterial(uint materialId, vec2 texCoords, float lod, out vec3 color, out float roughness, out float metalness)
{
    uint materialType;
    uint materialIndex = unpackMaterialId(materialId, materialType);

    if (materialType == MaterialTypeTextured)
    {
        const TexturedMaterial material = texturedMaterials[materialIndex];
        color = textureLod(textures[GetColorTextureIndex(0, material)], texCoords, lod).xyz;
        roughness = textureLod(textures[GetRoughnessTextureIndex(0, material)], texCoords, lod).y;
        metalness = textureLod(textures[GetMetalicTextureIndex(0, material)], texCoords, lod).z;
    }
    else
    {
        const SolidColorMaterial material = solidColorMaterials[materialIndex];
        color = material.Color;
        roughness = DefaultRoughness;
        metalness = DefaultMetalness;
    }
}

bool sampleLight(out vec3 lightDirection, out vec3 lightColor, out float lightDistance, out float attenuation, vec3 position, inout uint rngState)
{
    uint lightIndex = uint(rand(rngState) * (u_LightCount + 1));
    
    if (lightIndex >= u_LightCount)
    {
        lightDirection = u_DirectionalLight.Direction;
        lightColor = u_DirectionalLight.Color;
        lightDistance = DirectionalLightDistance;
        attenuation = 1.0f;
        return true;
    }

    const PointLight light = u_Lights[lightIndex];
    lightDistance = distance(position, light.Position);
    lightDirection = normalize(position - light.Position);
    lightColor = light.Color;
    attenuation = 1.0f / (light.AttenuationConstant + lightDistance * light.AttenuationLinear + lightDistance * lightDistance * light.AttenuationQuadratic);
    return false;
}

void main()
{
    // TODO: Pass frame number
    uint rng = initRng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, 0);

    const Camera camera = mainUniform.u_Camera;

    const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
    const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
    vec2 d = inUV * 2.0 - 1.0;

    vec3 origin = (camera.ViewInverse * vec4(0, 0, 0, 1)).xyz;
    vec3 target = (camera.ProjInverse * vec4(d.x, d.y, 1, 1)).xyz;
    vec3 direction = (camera.ViewInverse * vec4(normalize(target), 0)).xyz;

    float tmin = 0.00001;
    float tmax = 10000.0;

    uint flags = gl_RayFlagsNoneEXT;
    if ((s_RaygenFlags & RaygenFlagsForceOpaque) != RaygenFlagsNone)
        flags |= gl_RayFlagsOpaqueEXT;
    if ((s_RaygenFlags & RaygenFlagsCullBackFaces) != RaygenFlagsNone)
        flags |= gl_RayFlagsCullBackFacingTrianglesEXT;

    const float ambient = 0.05f;
    vec3 totalLight = vec3(0.0f);
    vec3 throughput = vec3(1.0f);

    const uint totalBounces = 4;
    for (int bounce = 0; bounce < totalBounces; bounce++)
    {
        traceRayEXT(u_TopLevelAS, flags, 0xff, PrimaryRayHitGroupIndex, 2, PrimaryRayMissGroupIndex, origin, tmin, direction, tmax, 0);

        if (payload.HitDistance == MISS_HIT_DISTANCE)
        {
            totalLight += throughput * payload.Position;
            break;
        }
            
        payload.Position = offsetRayOrigin(payload.Position, payload.Normal);

        const float lod = payload.Lod;

        vec3 color;
        float roughness, metalness;
        sampleMaterial(payload.MaterialId, payload.TexCoords, lod, color, roughness, metalness);

        const vec3 viewDir = direction;
        const vec3 V = -normalize(viewDir);
        const vec3 N = payload.Normal;
            
        vec3 lightDirection, lightColor;
        float attenuation, lightDistance;

        if (bounce == 0)
            totalLight += ambient * color;

        sampleLight(lightDirection, lightColor, lightDistance, attenuation, payload.Position, rng);

        // TODO: Scale down according to the probability
        if (!checkOccluded(lightDirection, payload.Position, lightDistance))
            totalLight += throughput * computeLightContribution(lightDirection, lightColor, attenuation, payload.Position, V, N, color, roughness, metalness);

        // TODO: Handle RenderModes

        throughput *= color;

        origin = payload.Position;
        direction = reflect(direction, N);

        // TODO: Sample proper BRDF
        vec3 offset = getRandomUnitSphere(rng);
        if (dot(N, offset) < 0)
            offset *= -1;
        direction = normalize(direction + offset * roughness);
    }

    imageStore(u_Image, ivec2(gl_LaunchIDEXT.xy), vec4(totalLight * mainUniform.u_Exposure, 1.0f));
}
