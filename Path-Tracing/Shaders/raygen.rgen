#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : require

#include "ShaderRendererTypes.incl"

layout(constant_id = RaygenFlagsConstantId) const uint s_RaygenFlags = RaygenFlagsNone;
layout(constant_id = HitGroupFlagsConstantId) const uint s_HitGroupFlags = HitGroupFlagsNone;

layout(binding = 0, set = 0) uniform accelerationStructureEXT u_TopLevelAS;
layout(binding = 1, set = 0, rgba32f) uniform image2D u_Image;
layout(binding = 2, set = 0) uniform MainBlock {
    RaygenUniformData mainUniform;
};

layout(binding = 3, set = 0) uniform sampler2D textures[];

layout(binding = 6, set = 0) readonly buffer TexturedMaterialBuffer {
    TexturedMaterial[] texturedMaterials;
};

layout(binding = 7, set = 0) readonly buffer SolidColorMaterialBuffer {
    SolidColorMaterial[] solidColorMaterials;
};

layout(binding = 8, set = 0) uniform LightsBuffer {
    uint u_LightCount;
    DirectionalLight u_DirectionalLight;
    PointLight[MaxLightCount] u_Lights;
};

layout(location = 0) rayPayloadEXT Payload payload;
layout(location = 1) rayPayloadEXT bool isOccluded;

#include "shading.glsl"
#include "tracing.glsl"

void sampleMaterial(uint materialId, vec2 texCoords, float lod, out vec3 color, out float roughness, out float metalness)
{
    uint materialType;
    uint materialIndex = unpackMaterialId(materialId, materialType);

    if (materialType == MaterialTypeTextured)
    {
        const TexturedMaterial material = texturedMaterials[materialIndex];
        color = textureLod(textures[GetColorTextureIndex(0, material)], texCoords, lod).xyz;
        roughness = textureLod(textures[GetRoughnessTextureIndex(0, material)], texCoords, lod).y;
        metalness = textureLod(textures[GetMetalicTextureIndex(0, material)], texCoords, lod).z;
    }
    else
    {
        const SolidColorMaterial material = solidColorMaterials[materialIndex];
        color = material.Color;
        roughness = DefaultRoughness;
        metalness = DefaultMetalness;
    }
}

bool sampleLight(out vec3 lightDirection, out vec3 lightColor, out float lightDistance, out float attenuation, vec3 position, inout uint rngState)
{
    uint lightIndex = uint(rand(rngState) * (u_LightCount + 1));
    
    if (lightIndex >= u_LightCount)
    {
        lightDirection = u_DirectionalLight.Direction;
        lightColor = u_DirectionalLight.Color;
        lightDistance = DirectionalLightDistance;
        attenuation = 1.0f;
        return true;
    }

    const PointLight light = u_Lights[lightIndex];
    lightDistance = distance(position, light.Position);
    lightDirection = normalize(position - light.Position);
    lightColor = light.Color;
    attenuation = 1.0f / (light.AttenuationConstant + lightDistance * light.AttenuationLinear + lightDistance * lightDistance * light.AttenuationQuadratic);
    return false;
}

struct Ray
{
    vec3 Origin;
    float tmin;
    vec3 Direction;
    float tmax;
};

Ray constructPrimaryRay(uvec2 pixel, uvec2 resolution, Camera camera)
{
    const vec2 pixelCenter = pixel + vec2(0.5);
    const vec2 inUV = pixelCenter / resolution;
    vec2 d = inUV * 2.0 - 1.0;

    vec3 origin = (camera.ViewInverse * vec4(0, 0, 0, 1)).xyz;
    vec3 target = (camera.ProjInverse * vec4(d.x, d.y, 1, 1)).xyz;
    vec3 direction = (camera.ViewInverse * vec4(normalize(target), 0)).xyz;

    float tmin = 0.00001;
    float tmax = 10000.0;

    return Ray(origin, tmin, direction, tmax);
}

void main()
{
    uint rng = initRng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, mainUniform.TotalSamples);

    const Ray primaryRay = constructPrimaryRay(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, mainUniform.Camera);

    uint flags = gl_RayFlagsNoneEXT;
    if ((s_RaygenFlags & RaygenFlagsForceOpaque) != RaygenFlagsNone)
        flags |= gl_RayFlagsOpaqueEXT;
    if ((s_RaygenFlags & RaygenFlagsCullBackFaces) != RaygenFlagsNone)
        flags |= gl_RayFlagsCullBackFacingTrianglesEXT;

    const float ambient = 0.05f;
    vec3 totalLight = vec3(0.0f);

    for (int smpl = 0; smpl < mainUniform.SampleCount; smpl++)
    {
        vec3 throughput = vec3(1.0f);
        Ray ray = primaryRay;

        for (int bounce = 0; bounce < mainUniform.BounceCount; bounce++)
        {
            traceRayEXT(u_TopLevelAS, flags, 0xff, PrimaryRayHitGroupIndex, 2, PrimaryRayMissGroupIndex, ray.Origin, ray.tmin, ray.Direction, ray.tmax, 0);

            if (payload.HitDistance == MISS_HIT_DISTANCE)
            {
                totalLight += throughput * payload.Position;
                break;
            }
            
            payload.Position = offsetRayOrigin(payload.Position, payload.Normal);

            vec3 color;
            float roughness, metalness;
            sampleMaterial(payload.MaterialId, payload.TexCoords, payload.Lod, color, roughness, metalness);

            const vec3 viewDir = ray.Direction;
            const vec3 V = -normalize(viewDir);
            const vec3 N = payload.Normal;
            
            vec3 lightDirection, lightColor;
            float attenuation, lightDistance;

            // if (mainUniform.TotalSamples == 0 && smpl == 0 && bounce == 0)
            //     totalLight += ambient * color;

            sampleLight(lightDirection, lightColor, lightDistance, attenuation, payload.Position, rng);

            // TODO: Scale down according to the probability
            if (!checkOccluded(lightDirection, payload.Position, lightDistance))
                totalLight += throughput * computeLightContribution(lightDirection, lightColor, attenuation, payload.Position, V, N, color, roughness, metalness);

            // TODO: Handle RenderModes

            throughput *= color;

            // TODO: Russian rulette termination

            ray.Origin = payload.Position;
            ray.Direction = reflect(ray.Direction, N);

            // TODO: Sample proper BRDF
            vec3 offset = getRandomUnitSphere(rng);
            if (offset == vec3(0.0f))
                offset = N;

            if (dot(N, offset) < 0)
                offset *= -1;
            ray.Direction = normalize(ray.Direction + offset * roughness);
        }
    }

    vec3 prevColor = imageLoad(u_Image, ivec2(gl_LaunchIDEXT.xy)).rgb;
    totalLight = (totalLight + prevColor * mainUniform.TotalSamples) / (mainUniform.TotalSamples + mainUniform.SampleCount);

    imageStore(u_Image, ivec2(gl_LaunchIDEXT.xy), vec4(totalLight * mainUniform.Exposure, 1.0f));
}
