#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_nonuniform_qualifier : require

#include "ShaderRendererTypes.incl"
#include "common.glsl"

layout(binding = 3, set = 0) uniform sampler2D textures[];

layout(binding = 5, set = 0) readonly buffer GeometryBuffer {
    Geometry[] geometries;
};

layout(binding = 6, set = 0) readonly buffer MetallicRoughnessMaterialBuffer {
    MetallicRoughnessMaterial[] metallicRoughnessMaterials;
};

layout(binding = 7, set = 0) readonly buffer SpecularGlossinessMaterialBuffer {
    SpecularGlossinessMaterial[] specularGlossinessMaterials;
};

layout(shaderRecordEXT, std430) buffer SBT {
    SBTBuffer sbt;
};

hitAttributeEXT vec3 attribs;

void main()
{
    const vec3 barycentricCoords = computeBarycentricCoords(attribs);

    VertexBuffer vertices = VertexBuffer(geometries[sbt.GeometryIndex].Vertices);
    IndexBuffer indices = IndexBuffer(geometries[sbt.GeometryIndex].Indices);

    const Vertex vertex = getInterpolatedVertex(vertices, indices, gl_PrimitiveID * 3, barycentricCoords);

    uint materialType;
    uint materialIndex = unpackMaterialId(sbt.MaterialId, materialType);

    const uint colorTextureIdx = materialType == MaterialTypeMetallicRoughness ?
        metallicRoughnessMaterials[materialIndex].ColorIdx : specularGlossinessMaterials[materialIndex].DiffuseIdx;
    const float alphaFactor = materialType == MaterialTypeMetallicRoughness ?
        metallicRoughnessMaterials[materialIndex].Color.a : 1.0f;

    const float alpha = texture(textures[colorTextureIdx], vertex.TexCoords).a * alphaFactor;

    if (alpha < 1.0f)
        ignoreIntersectionEXT;
}
